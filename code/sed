#!/usr/bin/env coffee

fs = require 'fs'
async = require 'async'
posixbre = require 'posixbre'
_ = require 'underscore'

# Process arguments and collect script.
scriptOption = false
script = ''
# The first two elements are "coffee" and the name of this script.
process.argv.shift()
process.argv.shift()
# true when output is suppressed with -n
suppressed = false
# Would love to use optimist to process options, but the order
# is important, and optimist destroys it. We have to process -e
# and -f in order.
while process.argv.length
  a = process.argv[0]
  if '-' != a[0]
    break
  else if '--' == a
    process.argv.shift()
    break
  else if '-e' == a
    process.argv.shift()
    script += process.argv.shift() + '\n'
    scriptOption = true
  else if '-f' == a
    process.argv.shift()
    script += fs.readFileSync(process.argv.shift())
    scriptOption = true
  else if '-n' == a
    suppressed = true
    process.argv.shift()
  else if '-' == a[0]
    usage()
    process.exit 4
if not scriptOption
  script = process.argv.shift()
# Set -n if script starts with #n.
if /^#n/.test script
  suppressed = true

if process.argv.length
  input = _.map process.argv, (name) ->
    -> fs.createReadStream name
else
  input = [ -> process.stdin ]

# If *s* (a string) has the form of a number, coerce it to
# an integer and return it; otherwise, return *s* unchanged.
intify = (s) ->
  x = Number(s)
  if not isNaN(x)
    return 0|x
  return s

# Table of writable files.
write = {}
mkWrite = (path) ->
  if not write[path]
    write[path] = fs.createWriteStream(path)
  return write[path]

# Remove a leading '\\' from x.
unEscape1 = (x) ->
  if x[0] == '\\'
    return x[1]
  return x

# Remove \c escape sequences from a string.
unEscape  = (s) ->
  s.replace /[^\\]|\\[\s\S]/g, unEscape1

# Replace \<newline> with newline.
trueNewline = (s) ->
  s.replace /[^\\]|\\\n/g, unEscape1

# Convert string to address, which is either '$', a number, or a
# RegExp instance.
asAddr = (s) ->
  if s == '$'
    return s
  if s is undefined
    return x
  x = intify s
  if typeof x == 'number'
    return x
  if x[0] in '\\/'
    if x[0] == '/'
      re = x[1..-2]
    else
      # handle \cfooc
      delim = x[0..1]
      x = x[2..-2]
      re = x.replace /[^\\]|\\./g, (c) ->
        if c == delim
          return c[1]
        return c
    if re == ''
      return 'empty'
    return RegExp posixbre.asRegExp re

nesting = 0
parseScript = (s) ->
  label = {}
  cmds = []
  while true
    [s, cmd] = parse1 s
    if s is null
      break
    cmds.push cmd
    if cmd.verb in ':}'
      label[cmd.arg] = cmds.length
  return [cmds, label]
# Parse the next command from string *s* returning a pair
# [*s*, *cmd*]
parse1 = (s) ->
  # The REs for addrs are not very right.
  re = ///
    (?:
      ( \d+ | \$ | (?:\\|(?=/))(.)(?:(?!\2)(?:[^\\\n]|\\.))*\2 ) # Addr1
      (?:,
      ( \d+ | \$ | (?:\\|(?=/))(.)(?:(?!\4)(?:[^\\\n]|\\.))*\4 ) # Addr2
      )? # Optional Addr2
    )?
    (\s*!+)? # m[5]                             # Optional !
    ([aci]\\|[bDdGgHhlNnPpqrstwxy#:=}{])        # Command
  ///g
  m = re.exec s
  if not m
    return [null, null]
  cmd = {}
  addr1 = asAddr m[1]
  addr2 = asAddr m[3]
  cmd.addr1 = addr1 if addr1?
  cmd.addr2 = addr2 if addr2?
  cmd.positive = not m[5]
  cmd.verb = m[6][0]
  s = s[re.lastIndex..]
  # Comments
  if cmd.verb in '#'
    s = s.replace /^.*?(\n|$)/, ''
  # Command blocks.
  # These are implemented as branches and labels. The labels all
  # start with '\n' because an ordinary label can't start with that.
  if cmd.verb == '{'
    cmd.arg = "\n#{nesting}"
    cmd.positive = !cmd.positive
    nesting += 1
  if cmd.verb == '}'
    nesting -= 1
    cmd.arg = "\n#{nesting}"
  # Commands that take 'a\' style arguments.
  if cmd.verb in 'aci'
    # Delete through newline.
    s = s.replace /^.*?\n/, ''
    argre = /((?:[^\n\\]|\\[\s\S])*)\n?/g
    m = argre.exec s
    cmd.arg = m[1].replace /\\[\s\S]/g, (x) -> x[1]
    s = s[argre.lastIndex..]
  # Commands that take single line arguments.
  if cmd.verb in ':btrw'
    argre = /[^\S\n]*(.*)\n?/g
    m = argre.exec s
    cmd.arg = m[1]
    if cmd.verb in 'w'
      cmd.stream = mkWrite cmd.arg
    s = s[argre.lastIndex..]
  # s function has a special argument form.
  # kinda of shared by y.
  if cmd.verb in 'sy'
    argre = ///
      ([^\\\n]) # delimiter, usually /
      ((?:(?!\1)[^\\\n]|\\.)*) # BRE
      \1
      ((?:(?!\1)[^\\\n]|\\[\s\S])*) # replacement
      \1
      ([\dgnpw]*) (?:[^\S\n]+(.+))? # flags
    ///g
    m = argre.exec s
    cmd.arg = m[1..5]
    cmd.delimiter = m[1]
    cmd.string1 = unEscape m[2]
    cmd.re = RegExp posixbre.asRegExp(m[2]), 'g'
    # Empty REs are slightly special.
    if m[2] == ''
      cmd.re = null
    cmd.replacement = m[3]
    cmd.replacement = trueNewline cmd.replacement
    if cmd.verb == 'y'
      cmd.replacement = unEscape cmd.replacement
    cmd.flags = m[4]
    digits = cmd.flags.match /\d+/
    if digits
      cmd.nth = +digits
    else
      cmd.nth = +not('g' in cmd.flags)
    cmd.file = m[5]
    if cmd.file
      cmd.stream = mkWrite cmd.file
    s = s[argre.lastIndex..]
    
  return [s, cmd]

[commands,label] = parseScript script
if /parse/.test process.env.SED_DEBUG
  console.warn "> commands"
  console.warn commands
  console.warn "> labels"
  console.warn label

class Sed
  constructor: () ->
    @currentLine = 0
    @pattern = null
    @hold = ''
    @finalLine = false
    # *indirectTo* is used (in eachLine) to determine what to do
    # (what continuation to call) when each line is read from input.
    # (Normally this is beginScript which starts a cycle of script
    # execution, but functions like 'N' can change that).
    @indirectTo = @beginScript

  addrMatch: (addr) ->
    if typeof addr is 'number'
      return @currentLine == addr
    if addr == '$'
      return @finalLine
    if addr == 'empty'
      addr = @lastAppliedRE
    if addr instanceof RegExp
      @apply addr
      return addr.test @pattern

  finalBuf: ->
    '''Process the final line of input (after the 'end' event
    has been handled).
    '''
    lines = buf.split '\n'
    lines.pop()
    if lines.length
      @finalLine = true
      eachLine lines[0], ->

  fileEnd: ->
    @finalBuf()

  beginScript: (line, nextLine) ->
    @inputLine line
    # List of delayed functions to call to append stuff after the
    # cycle output (typically 'a' and 'r' verbs).
    appends = []

    endCycle = () =>
      # Pattern space may have been deleted (EG 'c' function).
      if not suppressed and @pattern isnt null
        process.stdout.write @pattern + '\n'
      @pattern = null
      for append in appends
        process.stdout.write append()
      if @quitter
        # :todo: What should the exit status be?
        process.exit()
      nextLine()

    evalCommand = (cmd, nextCmd) =>
      [execute, endRange] = @evalAddr cmd

      if execute == cmd.positive
        return @evalFunction cmd, endRange, appends, nextCmd, nextLine
      nextCmd()

    @cmdIndex = 0
    script1 = (stop) =>
      # If *stop* is true then we abort the command loop early
      # and begin the next cycle.
      if @cmdIndex >= commands.length or stop
        return endCycle()
      evalCommand commands[@cmdIndex++], (x) ->
        setImmediate script1, x

    script1()

  evalAddr: (cmd) ->
    endRange = false
    # 0 address.
    if not cmd.addr1 and not cmd.addr2
      execute = true
      endRange = true
    # One address.
    if cmd.addr1 and not cmd.addr2
      execute = @addrMatch cmd.addr1
      endRange = true
    # Two address.
    if cmd.addr1 and cmd.addr2
      if not cmd.flipped
        execute = false
        if @addrMatch cmd.addr1
          execute = true
          cmd.flipped = true
          # There is a bit of a special case for numeric 2nd addrs
          if typeof cmd.addr2 == 'number'
            if cmd.addr2 <= @currentLine
              cmd.flipped = false
              endRange = true
      else
        execute = true
        if @addrMatch cmd.addr2
          cmd.flipped = false
          endRange = true
    return [execute, endRange]

  evalFunction: (cmd, endRange, appends, nextCmd, nextLine) ->
    if '=' == cmd.verb
      process.stdout.write lineNumber + '\n'
    if '{' == cmd.verb
      @branch cmd.arg
    if 'a' == cmd.verb
      appends.push -> (cmd.arg + '\n')
    if 'b' == cmd.verb
      @branch cmd.arg
    if 'c' == cmd.verb
      @pattern = null
      if endRange
        process.stdout.write cmd.arg + '\n'
      return nextCmd 'cycle'
    if 'D' == cmd.verb
      if '\n' in @pattern
        @pattern = @pattern.replace /^.*?\n/, ''
      else
        @pattern = null
      return nextCmd 'cycle'
    if 'd' == cmd.verb
      @pattern = null
      return nextCmd 'cycle'
    if 'G' == cmd.verb
      @pattern += '\n' + @hold
    if 'g' == cmd.verb
      @pattern = @hold
    if 'H' == cmd.verb
      @hold += '\n' + @pattern
    if 'h' == cmd.verb
      @hold = @pattern
    if 'i' == cmd.verb
      process.stdout.write cmd.arg + '\n'
    if 'l' == cmd.verb
      x = @pattern.replace /[\s\S]/g, (s) ->
        switch s
          when '\\'
            return '\\\\'
          when '\x07' # Probably what \a is.
            return '\\a'
          when '\b'
            return '\\b'
          when '\f'
            return '\\f'
          when '\r'
            return '\\r'
          when '\t'
            return '\\t'
          when '\v'
            return '\\v'
          when '\n'
            return '$\n'
          else
            return s
      process.stdout.write x + '$\n'
    if 'N' == cmd.verb
      @indirectTo = (line) =>
        @inputLine line
        @indirectTo = @beginScript
        nextCmd()
      return nextLine()
    if 'n' == cmd.verb
      @indirectTo = (line) =>
        @inputLine line
        @indirectTo = @beginScript
        nextCmd()
      return nextCmd 'cycle'
    if 'P' == cmd.verb
      if '\n' in @pattern
        process.stdout.write @pattern[..@pattern.indexOf '\n']
      else
        process.stdout.write @pattern + '\n'
    if 'p' == cmd.verb
      process.stdout.write @pattern + '\n'
    if 'q' == cmd.verb
      @quitter = true
      return nextCmd 'cycle'
    if 'r' == cmd.verb
      appends.push -> fs.readFileSync cmd.arg
    if 's' == cmd.verb
      @verbs cmd
    if 't' == cmd.verb
      if @substituted
        @branch cmd.arg
      @substituted = false
    if 'w' == cmd.verb
      cmd.stream.write @pattern + '\n'
    if 'x' == cmd.verb
      [@pattern, @hold] = [@hold, @pattern]
    if 'y' == cmd.verb
      @pattern = @pattern.replace /[\s\S]/g, (x) ->
        i = cmd.string1.indexOf x
        if i >= 0
          cmd.replacement[i]
        else
          x
    nextCmd()

  apply: (re) ->
    @lastAppliedRE = re

  verbs: (cmd) ->
    re = cmd.re
    if re is null
      re = @lastAppliedRE
    re.lastIndex = 0
    matchCount = 0
    substituted = false
    result = []
    lastIndex = 0
    @apply re
    while true
      m = re.exec @pattern
      if not m
        break
      matchCount += 1
      if 'g' in cmd.flags or cmd.nth == matchCount
        substituted = @substituted = true
        repl = cmd.replacement.replace /\\.|[^\\]/g, (particle) ->
          if particle == '&'
            return m[0]
          if particle[0] != '\\'
            return particle
          # particle[0] is '\\'
          if particle[1] in '&\\'
            return particle[1]
          if /[1-9]/.test particle[1]
            return m[particle[1]] or ''
        result = result.concat [
          @pattern.slice(lastIndex, m.index)
          repl
        ]
        lastIndex = re.lastIndex
        if m.index == re.lastIndex
          # Zero-width match.
          re.lastIndex += 1
      if cmd.nth and matchCount >= cmd.nth
        break
    if substituted
      result.push @pattern.slice lastIndex
      @pattern = result.join ''
      if 'p' in cmd.flags
        process.stdout.write @pattern + '\n'
      if 'w' in cmd.flags
        cmd.stream.write @pattern + '\n'

  inputLine: (line) ->
    """Appends the line to pattern space, and updates
    various flags and counters."""
    if @pattern is null
      @pattern = line
    else
      @pattern += '\n' + line
    @currentLine += 1
    # Keeps track for the 't' command.
    @substituted = false

  branch: (label) ->
    """Implements b and t commands."""
    if /branch/.test process.env.SED_DEBUG
      console.warn "> branch to [[#{label}]]"
    if not label
      @cmdIndex = Infinity
    else
      index = @label[label]
      if index?
        @cmdIndex = index
      else
        console.warn "Branch label [[#{label}]] is missing."
        @cmdIndex = Infinity

sed = new Sed
sed.label = label
lineNumber = 0

eachLine = (line, cb) ->
  lineNumber += 1
  return sed.indirectTo line, cb

# Buffer into which data is collected until we have more than
# one line (or one final line).
buf = ''
# *processing* and *ended* keep track of whether we are in
# the middle of a 'data' event when we get an 'end' event.
processing = false
ended = false
inputData = (data) ->
  @pause()
  processing = true
  buf += data
  lines = buf.split '\n'
  buf = lines.pop()
  if buf == '' and lines.length
    # Retain last line of data, as it might be the final
    # line of input.
    buf = lines.pop() + '\n'
  async.eachSeries lines, eachLine, () =>
    processing = false
    @resume()
    if ended
      # Must have got 'end' event while processing
      newStream()
inputEnd = () ->
  ended = true 
  if not processing
    newStream()

currentStream = null
newStream = () ->
  ended = false
  getStream = input.shift()
  if getStream
    currentStream = getStream()
    currentStream.on 'data', inputData
    currentStream.on 'end', inputEnd
  else
    sed.finalBuf()

newStream()
