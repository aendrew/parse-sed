#!/bin/sh
# Adds two decimal numbers

alias sed=${SED:-sed}

printf '%s\n' "${*-$(cat)}" | sed -n '
s/[0-9]\{1,\}/&,/g
s/,/;/
:loop
  /[1-9],/b dec
:shift
  / ,/b quit
  /^;/b quit
  s/\(.\)\([,;]\)/\2\1/g
  b loop
:dec
  # Decrement digit before comma, increment number with semicolon.
  # Ideas for implementation from http://lvogel.free.fr/sed.htm
  s/,/,0123456789,/;s/\(.\),[^,]*\(.\)\1[^,]*,/\2,/
  s/;/;:/
  :carry
    s/^;/0;/
    s/;/;0123456789a;/;s/\(.\);[^;]*\1\(.\)[^;]*;/\2/
    s/a/;0/
    t carry
  s/:/;/
  b loop

:quit
  s/ *,.*//
  s/;//
p'
